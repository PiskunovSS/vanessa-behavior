&НаСервере
Процедура ВосстановитьНастройки(ПараметрЗапуска)
	
	Настройки = ХранилищеОбщихНастроек.Загрузить("VanessaLoader");
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		Настройки.Свойство("ПутьКVanessaBehavior", 				Объект.ПутьКVanessaBehavior);
		Настройки.Свойство("ЗапускатьVBПриОткрытии", 			Объект.ЗапускатьVBПриОткрытии);
		Настройки.Свойство("НеЗакрыватьПриВыполненииСценариев", Объект.НеЗакрыватьПриВыполненииСценариев);
		Настройки.Свойство("ДополнительныеПараметрыМенеджера", Объект.ДополнительныеПараметрыМенеджера);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Настройки = Новый Структура;
	Настройки.Вставить("ПутьКVanessaBehavior", 				Объект.ПутьКVanessaBehavior);		
	Настройки.Вставить("ЗапускатьVBПриОткрытии", 			Объект.ЗапускатьVBПриОткрытии);
	Настройки.Вставить("НеЗакрыватьПриВыполненииСценариев", Объект.НеЗакрыватьПриВыполненииСценариев);
	Настройки.Вставить("ДополнительныеПараметрыМенеджера", 	Объект.ДополнительныеПараметрыМенеджера);
	
	ХранилищеОбщихНастроек.Сохранить("VanessaLoader",, Настройки);
	Модифицированность = Ложь;
	
КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВосстановитьНастройки(ПараметрЗапуска);	
	
	Если Объект.ЗапускатьVBПриОткрытии тогда
		
		КомандаЗапуститьМенеджерТестированияСVB(Неопределено);
		
	КонецЕсли;
	
	ОбновитьСтрокуЗапуска();
	КомандаОбновитьПараметрыЗапуска(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьМенеджерТестированияСVB()
	
	ОбновитьСтрокуЗапуска();	
	
	ЗапуститьСистему(СтрокаЗапуска);	
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьСтрокуЗапуска()
	
	ЭтоLinux = Ложь;
	СисИнфо = Новый СистемнаяИнформация;
	Если Найти(Строка(СисИнфо.ТипПлатформы), "Linux")>0 Тогда 
		ЭтоLinux = Истина;
	КонецЕсли;
	
	КаталогБазы = СтрокаСоединенияИнформационнойБазы();
	
	СтрокаЗапуска1с = КаталогПрограммы() + "1cv8c";
	Если ЭтоLinux = Ложь Тогда
		СтрокаЗапуска1с = СтрокаЗапуска1с + ".exe";
	КонецЕсли;
	
	СвободныйПорт = 1538;
	
	Если Найти(ВРег(КаталогБазы),ВРег("File=")) > 0 Тогда
		КаталогБазы = СтрЗаменить(КаталогБазы,"File="," /F");
	Иначе
		КаталогБазы = СтрЗаменить(КаталогБазы,"Srvr=","/S");
		КаталогБазы = СтрЗаменить(КаталогБазы,""";Ref=""","\");
	КонецЕсли;
	КаталогБазы = СтрЗаменить(КаталогБазы,";","");
	
	ПараметрыОтладчика = "";
	
	Если ЭтоLinux = Истина Тогда
		КаталогБазы = СтрЗаменить(КаталогБазы, "\", "/");
	КонецЕсли;

	СтрокаЗапуска = """" + СтрокаЗапуска1с + """ ENTERPRISE " + КаталогБазы
		+ ПараметрыОтладчика + " /TESTMANAGER "
	    + " /EXECUTE """ + Объект.ПутьКVanessaBehavior + """ ";
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗапуститьМенеджерТестированияСVB(Команда)
	Если ЗначениеЗаполнено(Объект.ПутьКVanessaBehavior) тогда
		ЗапуститьМенеджерТестированияСVB();
	Иначе
		Сообщить("Не указан путь к обработке VanessaBehavior.epf!");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы = Неопределено тогда
		ЗавершениеРаботы = Ложь;
	КонецЕсли;	
	
	Если не ЗавершениеРаботы тогда
		
		Если Объект.НеЗакрыватьПриВыполненииСценариев тогда
			Отказ = Истина;
		КонецЕсли;	
		
		Если Модифицированность тогда
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтаФорма, Параметры);
			ПоказатьВопрос(Оповещение, "Сохранить измененные настройки запуска обработки?", Режим, 0);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
        СохранитьНастройки();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьПараметрыЗапуска(Команда)
	
	ИспользуемоеИмяФайла = ПолучитьИспользуемоеИмяФайлаОбработки();
	
	ДополнительныеПараметрыЗапускаИБ = "/EXECUTE """ + ИспользуемоеИмяФайла + """";
		
КонецПроцедуры

&НаСервере
функция ПолучитьИспользуемоеИмяФайлаОбработки()
	
	Возврат РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла;
		
КонецФункции

&НаКлиенте
Процедура ПутьКVanessaBehaviorНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	
	ДиалогВыбораФайла.Показать(Вычислить("Новый ОписаниеОповещения(""ПутьКVanessaBehaviorНачалоВыбораЗавершение"", ЭтаФорма)"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКVanessaBehaviorНачалоВыбораЗавершение(ВыбранныеФайлы, ТекущиеДанные) Экспорт
	Если Ложь
		Или ВыбранныеФайлы = Неопределено
		Или ВыбранныеФайлы.Количество() = 0
		Тогда
		
		Возврат;
	КонецЕсли;
	
	Объект.ПутьКVanessaBehavior = ВыбранныеФайлы[0];
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСохранитьНастройки(Команда)
	
	СохранитьНастройки();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКVanessaBehaviorПриИзменении(Элемент)
	
	Модифицированность = Истина;
	ОбновитьСтрокуЗапуска();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускатьVBПриОткрытииПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрыть(Команда)
	
	Объект.НеЗакрыватьПриВыполненииСценариев = Ложь;
	Модифицированность = Ложь;
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры
